include "global.tbh"

const IOTHUB_INIT_SIGNATURE=&h1FD3
const TOPIC_METHOD="$iothub/methods/POST/"
const TOPIC_PROERTY_DESIRED="$iothub/twin/PATCH/properties/desired/"
const TOPIC_PROERTY_REPORTED="$iothub/twin/PATCH/properties/reported/"
const TOPIC_TWIN="$iothub/twin/"
const TOPIC_STREAMS="$iothub/streams/POST/"
const TOPIC_RESULT="$iothub/twin/res/"
const API_VERSION="/?api-version=2018-06-30"
const TOPIC_TIMESTAMP="iothub-creation-time-utc="

dim iothub_telemetry(IOTHUB_TELEMETRY_NUM) as struct_iothub_telemetry
dim iothub_property(IOTHUB_PROPERTY_NUM) as struct_iothub_property
dim iothub_command(IOTHUB_COMMAND_NUM) as struct_iothub_command

dim iothub_state as en_iothub_states
dim iothub_interface as pl_sock_interfaces
dim iothub_rid as dword=0
dim iothub_sending_telemetry as no_yes = NO
dim device_bound_message_topic as string(64)
dim server_bound_message_topic as string
dim iothub_init_flag as word
dim num_of_suback as byte
dim iothub_dps_sock as byte
dim iothub_dps_server as string(64)=""
dim iothub_assigned_hub as string(64)=""
dim iothub_scope_id as string(16)
dim iothub_device_id as string(32)
dim iothub_primary_key as string(64)
dim iothub_sas_token as string
dim iothub_timer as byte=0
dim iothub_dps_authorization as string

declare mqtt_sock_num as byte
declare function iothub_init() as ok_ng
declare sub iothub_debugprint(byref print_data as string)
declare function iothub_get_descriptor_field(line_end_pos as word, byref field_start_pos as word) as string
declare sub iothub_dps_device_lookup()
declare sub iothub_dps_device_provisioning()
declare sub iothub_dps_connect(byref target_ip as string)

function iothub_init() as ok_ng
	const END_OF_LINE="\x0d"
	dim i as word
	dim s as string
	dim telemtry_index as byte=0
	dim property_index as byte=0
	dim command_index as byte=0
	dim item_type as string(1)
	dim field_start_pos, line_end as word
	dim pos as byte
	
	iothub_init=NG
	romfile.open(IOTHUB_DESCRIPTOR_FILE)
	if romfile.size=0 then
		#if IOTHUB_DEBUG_PRINT
			iothub_debugprint("ERROR: '"+IOTHUB_DESCRIPTOR_FILE+"' is not in your project or file is empty.")
		#endif
		exit function
	end if

	i=romfile.find(romfile.pointer,">>",1)
	while i<>0
		'we are now at the ">>" pointing at the beginning of one setting descriptor line
		romfile.pointer=i+2
		
		'find the end of this setting descriptor line (or the end of file)
		line_end=romfile.find(romfile.pointer,END_OF_LINE,1)
		if line_end=0 then
			line_end=romfile.size+1
		end if
		
		'extract item name
		s=iothub_get_descriptor_field(line_end,field_start_pos)			
		if s="" then
			'missing item name field
			#if IOTHUB_DEBUG_PRINT
				iothub_debugprint("ERROR: missing name field.")
			#endif
			
			exit function
		end if
		
		if len(s)>IOTHUB_ITEM_NAME_LEN then
			#if IOTHUB_DEBUG_PRINT
				iothub_debugprint("ERROR: item name length is "+str(len(s))+", while you have 'IOTHUB_ITEM_NAME_LEN "+str(IOTHUB_ITEM_NAME_LEN)+"'.")
			#endif
			exit function
		end if
		
		item_type=iothub_get_descriptor_field(line_end,field_start_pos)
		select case item_type
		case "T":
			if telemtry_index<IOTHUB_TELEMETRY_NUM then
				iothub_telemetry(telemtry_index).name=s				
			else
				#if IOTHUB_DEBUG_PRINT
					iothub_debugprint("ERROR: telemtry exceed pre-defined number.")
				#endif
				exit function
			end if
		case "P":
			if property_index<IOTHUB_PROPERTY_NUM then
				iothub_property(property_index).name=s
			else
				#if IOTHUB_DEBUG_PRINT
					iothub_debugprint("ERROR: property exceed pre-defined number.")
				#endif
				exit function
			end if
			
		case "C":
			if command_index<IOTHUB_COMMAND_NUM then
				iothub_command(command_index).name=s
				
			else
				#if IOTHUB_DEBUG_PRINT
					iothub_debugprint("ERROR: command exceed pre-defined number.")
				#endif
				exit function
			end if
			
		case else
			
			#if IOTHUB_DEBUG_PRINT
				iothub_debugprint("ERROR: unknow item type.")
			#endif
			exit function		
		end select

		select case item_type
		case "T":
			iothub_telemetry(telemtry_index).value=""
			telemtry_index=telemtry_index+1				
		case "P":
			s=iothub_get_descriptor_field(line_end,field_start_pos)	
			pos=instr(1,s,",",1)
			if pos>0 then
				if right(s,len(s)-pos)="W" then
					iothub_property(property_index).writable=YES
				else
					iothub_property(property_index).writable=NO
				end if
				s=left(s,pos-1)
			end if
				
			if stg_find(s)>0 then
				iothub_property(property_index).stg_name=s
				property_index=property_index+1
			else
				#if IOTHUB_DEBUG_PRINT
					iothub_debugprint("ERROR: unknow setting "+s)
				#endif
				exit function			
			end if
			
		case "C":
			s=iothub_get_descriptor_field(line_end,field_start_pos)	
			iothub_command(command_index).addr=field_start_pos
			command_index=command_index+1
		case else:	
		end select
		i=romfile.find(romfile.pointer,">>",1)
	wend
	
	dns_start(sock_get("DNS"))
	
	sock.num=sock.tlscurrentnum
	sock.tlsdeinit()
	
	iothub_dps_sock=sock_get("DPS")
	sock.num=iothub_dps_sock
	sock.connectiontout=60
	sock.protocol=PL_SOCK_PROTOCOL_TCP
	sock.inconmode= PL_SOCK_INCONMODE_ANY_IP_ANY_PORT
	sock.targetport=443
	sock.allowedinterfaces = sock.availableinterfaces	
	sock.txbuffrq(8)
	sock.rxbuffrq(8)
	sys.buffalloc()
	
	iothub_rid=0
	iothub_init_flag=IOTHUB_INIT_SIGNATURE
	iothub_init=OK	
end function

public sub iothub_start(byref device_id as string, byref scope_id as string, byref dps_url as string, byref symmetricKey as string, interface as pl_sock_interfaces)
	dim resourceUri as string=scope_id+"%2Fregistrations%2F"+device_id

	if device_id="" or scope_id="" or dps_url="" or symmetricKey="" then
		#if IOTHUB_DEBUG_PRINT
			iothub_debugprint("ERROR: iothub_start() invalid parameters")
		#endif
		exit sub
	end if
	iothub_init()
	iothub_device_id=device_id
	iothub_scope_id=scope_id
	iothub_dps_server=dps_url
	iothub_primary_key =symmetricKey
	iothub_interface=interface
	server_bound_message_topic="devices/"+iothub_device_id+"/messages/events/"
	device_bound_message_topic="devices/"+iothub_device_id+"/messages/devicebound/"

	iothub_state=EN_IOTHUB_IDLE
	iothub_dps_authorization=generateSasToken(resourceUri, iothub_primary_key, 525600)
end sub

public sub iothub_on_connected()

	if iothub_init_flag<>IOTHUB_INIT_SIGNATURE then
		#if IOTHUB_DEBUG_PRINT
			iothub_debugprint("ERROR: iothub_init() wasn't used or failed")
		#endif
		exit sub
	end if

	mqtt_sub(device_bound_message_topic+"#",QOS_AT_MOST_ONCE)
	mqtt_sub(TOPIC_METHOD+"#",QOS_AT_MOST_ONCE)
	mqtt_sub(TOPIC_RESULT+"#",QOS_AT_MOST_ONCE)
	mqtt_sub(TOPIC_PROERTY_DESIRED+"#",QOS_AT_MOST_ONCE)
	mqtt_sub(TOPIC_STREAMS+"#",QOS_AT_MOST_ONCE)

	#if IOTHUB_DEBUG_PRINT
		iothub_debugprint("Server connected.")
	#endif
	iothub_state=EN_IOTHUB_CONNECTED
end sub

public sub iothub_on_disconnected()
	
	if iothub_init_flag<>IOTHUB_INIT_SIGNATURE then
		#if IOTHUB_DEBUG_PRINT
			iothub_debugprint("ERROR: iothub_init() wasn't used or failed")
		#endif
		exit sub
	end if
	dns_stop()
	dns_start(sock_get("DNS"))
	mqtt_connect()
	#if IOTHUB_DEBUG_PRINT
		iothub_debugprint("Disconnected from server.")
	#endif
	iothub_state=EN_IOTHUB_DISCONNECTED
end sub

public sub iothub_proc_mqtt_msg(byref topic as string,byref data as string)
	dim topic_len as byte=len(TOPIC_METHOD)
	dim pos1, pos2, pos3 as byte=0
	dim item_name as string
	dim value as string
	dim s as string
	dim rid as string
	dim i as byte
	dim c as byte=0
	
	if iothub_init_flag<>IOTHUB_INIT_SIGNATURE then
		#if IOTHUB_DEBUG_PRINT
			iothub_debugprint("ERROR: iothub_init() wasn't used or failed")
		#endif
		exit sub
	end if
	
	if instr(1,topic,TOPIC_METHOD,1)>0 then
		pos1=instr(topic_len+1,topic,"/",1)
		item_name=mid(topic,topic_len+1,pos1-topic_len-1)
		#if IOTHUB_COMMAND_NUM>0
		for i=0 to IOTHUB_COMMAND_NUM-1
			if iothub_command(i).name=item_name then
				pos1=instr(topic_len+1,topic,"$rid=",1)
				rid=right(topic, len(topic)-pos1-4)
				iothub_command(i).rid=lval(rid)
				if iothub_command(i).rid > iothub_rid then
					iothub_rid=iothub_command(i).rid
				end if
				callback_iothub_command_triggered(item_name,data)
				#if IOTHUB_DEBUG_PRINT
					iothub_debugprint("Command: "+item_name+" Data: "+data)
				#endif				
				exit sub
			end if
		next i
		#endif
		exit sub
	end if
	
	#if IOTHUB_PROPERTY_NUM>0
	if instr(1, topic,TOPIC_TWIN,1)>0 then
		#if IOTHUB_DEBUG_PRINT
			iothub_debugprint("Properties update: "+data)
		#endif		
		pos1=instr(1,data,"desired",1)
		do 
			pos2=instr(pos1,data,"{",1)
			pos3=instr(pos1,data,"}",1)
			if pos2>0 and pos2<pos3 then
				pos1=pos2+1
				c=c+1
			else
				if pos3>0 then
					c=c-1
					if c=0 then
						s=mid(data,pos1+1,pos3-pos1-1)
					end if
					pos1=pos3+1
				end if
			end if
			if pos2=0 and pos3=0 then
				c=0
			end if			
		loop while c>0

		pos1=1
		do 
			pos2=instr(pos1,s,":",1)
			if pos2>0 then
				item_name=mid(s,pos1,pos2-pos1-1)			
				pos3=instr(pos2,s,",",1)
				if pos3=0 then
					pos3=len(s)+1
				end if
				value=mid(s,pos2+1,pos3-pos2-1)
				
				
					for i =0 to IOTHUB_PROPERTY_NUM-1
						if iothub_property(i).name=item_name then
							if value<>stg_get(iothub_property(i).stg_name,0) then
								#if IOTHUB_DEBUG_PRINT
									iothub_debugprint("Remote value of \x22"+item_name+"\x22 was "+stg_get(iothub_property(i).stg_name,0)+" change to: "+value)
								#endif							
								pos1=instr(1,topic,"$rid=",1)
								if pos1>0 then
									rid=right(topic, len(topic)-pos1-4)
									iothub_rid=lval(rid)
								end if
								
								stg_set(iothub_property(i).stg_name,0,value)
								value="{\x22"+item_name+"\x22:"+value+"}"
								iothub_rid=iothub_rid+1
								mqtt_publish(TOPIC_PROERTY_REPORTED+"?$rid="+str(iothub_rid),value,QOS_AT_MOST_ONCE)
							end if
						end if
					next i				
					pos1=pos3+1
				
			end if
		loop while pos2>0		
	end if
	#endif
end sub

public sub iothub_command_respond(byref command as string, byref returns as string, result as en_iothub_command_codes)
	dim i as byte
	dim topic as string

	if iothub_init_flag<>IOTHUB_INIT_SIGNATURE then
		#if IOTHUB_DEBUG_PRINT
			iothub_debugprint("ERROR: iothub_init() wasn't used or failed")
		#endif
		exit sub
	end if
	#if IOTHUB_COMMAND_NUM>0
	for i=0 to IOTHUB_COMMAND_NUM-1
		if iothub_command(i).name=command then
			if returns="" then
				returns="{}"
			end if
			topic="$iothub/methods/res/"+str(result)+"/?$rid="+str(iothub_command(i).rid)
			mqtt_publish(topic,returns,QOS_AT_MOST_ONCE)
		end if
	next i
	#endif
end sub

public sub iothub_update_telemetry(byref name as string, byref value as string)
	dim i as byte
	
	if iothub_init_flag<>IOTHUB_INIT_SIGNATURE then
		#if IOTHUB_DEBUG_PRINT
			iothub_debugprint("ERROR: iothub_init() wasn't used or failed")
		#endif
		exit sub
	end if
	
	for i=0 to IOTHUB_TELEMETRY_NUM-1
		if iothub_telemetry(i).name=name then
			iothub_telemetry(i).value=value
			iothub_telemetry(i).updated=YES
			exit sub
		end if
	next i
end sub

public sub iothub_send_telemetry(byref timestamp as string)
	dim i as byte
	dim s as string
	dim value as string
	dim attribute as string(IOTHUB_ITEM_NAME_LEN)
	dim pos as byte
	
	if iothub_init_flag<>IOTHUB_INIT_SIGNATURE then
		#if IOTHUB_DEBUG_PRINT
			iothub_debugprint("ERROR: iothub_init() wasn't used or failed")
		#endif
		exit sub
	end if

	s=""
	for i=0 to IOTHUB_TELEMETRY_NUM-1
		if iothub_telemetry(i).updated=YES then
			iothub_telemetry(i).updated=NO
			attribute=iothub_telemetry(i).name
			value=iothub_telemetry(i).value
			s=iothub_json_helper(s,attribute,value)
		end if
	next i
	
	if timestamp<>"" then
		pos=instr(1,server_bound_message_topic,TOPIC_TIMESTAMP,1)
		if pos>0 then
			server_bound_message_topic=left(server_bound_message_topic,pos-1)+TOPIC_TIMESTAMP+timestamp
		else
			server_bound_message_topic=server_bound_message_topic+TOPIC_TIMESTAMP+timestamp
		end if
	end if
	sock.num=mqtt_sock_num
	sock.notifysent(0)
	mqtt_publish(server_bound_message_topic,s,QOS_AT_MOST_ONCE)
	iothub_sending_telemetry=YES
end sub

public sub iothub_proc_timer()

	dim b as byte
	
	b=sock.num
	sock.num=iothub_dps_sock

	select case iothub_state
	case EN_IOTHUB_REGISTERED:
		sock_release(iothub_dps_sock)
	
		dim user_name as string=iothub_assigned_hub+"/"+iothub_device_id+API_VERSION		
		dim resourceUri as string= iothub_assigned_hub+"%2Fdevices%2F"+iothub_device_id
		dns_start(sock_get("DNS"))
		iothub_sas_token="SharedAccessSignature sr="+iothub_assigned_hub+"%2Fdevices%2F"+iothub_device_id+"&sig="+ generateSasToken(resourceUri, iothub_primary_key, 525600)
		mqtt_start(PL_SOCK_INTERFACE_WLN,iothub_assigned_hub,8883,user_name,iothub_sas_token,iothub_device_id,true)
		iothub_state=EN_IOTHUB_CONNECTING
		
	case EN_IOTHUB_IP_OBTAINED:
		if iothub_assigned_hub="" then
			if dns_connect(iothub_interface,"",0) = EN_STATUS_DNS_OK then
				dns_query(iothub_dps_server)	
			end if
			iothub_state=EN_IOTHUB_DPS_CONNECTING
		else

			iothub_state=EN_IOTHUB_CONNECTED
		end if
		iothub_timer=40

	case EN_IOTHUB_CONNECTING,EN_IOTHUB_DISCONNECTED, EN_IOTHUB_CONNECTED:
	
		mqtt_proc_timer()
	end select
	
	if iothub_timer>0 then
		iothub_timer=iothub_timer-1
		if iothub_timer=0 then
			select case iothub_state
			case EN_IOTHUB_IDLE:
				
			case EN_IOTHUB_DPS_CONNECTING:
				callback_iothub_dps_failed()
				if iothub_assigned_hub="" then
					dns_stop()
					dns_start(sock_get("DNS"))
					iothub_state=EN_IOTHUB_IP_OBTAINED
				end if
			case EN_IOTHUB_DEVICE_LOOKUP:
				if sock.state=PL_SST_EST_TLS then
					iothub_dps_device_lookup()
				else
					iothub_dps_connect("")
				end if
				iothub_timer=10
				
			case EN_IOTHUB_PROVISIONING:
				if sock.state=PL_SST_EST_TLS then
					iothub_dps_device_provisioning()
				else
					iothub_dps_connect("")
					iothub_state=EN_IOTHUB_DPS_CONNECTING
				end if

			end select			
		end if
	end if
	sock.num=b
	
end sub

public sub iothub_proc_sock_event()
	if iothub_init_flag<>IOTHUB_INIT_SIGNATURE then
		#if IOTHUB_DEBUG_PRINT
			iothub_debugprint("ERROR: iothub_init() wasn't used or failed")
		#endif
		exit sub
	end if

	select case iothub_state
		
		case EN_IOTHUB_IDLE, EN_IOTHUB_DPS_CONNECTING, EN_IOTHUB_DEVICE_LOOKUP, EN_IOTHUB_PROVISIONING:
		if sock.num=iothub_dps_sock then
			select case sock.state
			case PL_SST_CLOSED, PL_SST_CL_PCLOSED,PL_SST_CL_PRESET_EST:
				sys.debugprint("dps disconnected\x0a")
				dim resourceUri as string=iothub_scope_id+"%2Fregistrations%2F"+iothub_device_id
				iothub_dps_authorization=generateSasToken(resourceUri, iothub_primary_key, 525600)

				callback_iothub_dps_failed()
				iothub_timer=5
				
			case PL_SST_EST_AOPENED:
				sock.tlshandshake(iothub_dps_server)
				
			case PL_SST_EST_TLS:
				iothub_timer=5
				select case iothub_state
				case EN_IOTHUB_DPS_CONNECTING:
					iothub_state=EN_IOTHUB_DEVICE_LOOKUP
					
				case EN_IOTHUB_DEVICE_LOOKUP:
					iothub_state=EN_IOTHUB_PROVISIONING
					
				end select				
			end select
		end if
	end select		

end sub

public sub iothub_proc_data()
	dim s as string
	dim pos1, pos2 as byte
	
	if iothub_init_flag<>IOTHUB_INIT_SIGNATURE then
		#if IOTHUB_DEBUG_PRINT
			iothub_debugprint("ERROR: iothub_init() wasn't used or failed")
		#endif
		exit sub
	end if

	select case iothub_state
	case EN_IOTHUB_IDLE:	
	case EN_IOTHUB_DPS_CONNECTING:
	case EN_IOTHUB_DEVICE_LOOKUP:
		if sock.num<>iothub_dps_sock then
			exit sub
		end if
		if iothub_state=EN_IOTHUB_DEVICE_LOOKUP then
			s=sock.tlsgetdata(255)
			pos1=instr(1,s,"\x0d\x0aDate: ",1)
			if pos1>0 then
				pos2=instr(pos1+3,s,"\x0d\x0a",1)
				if pos2>0 then
					s=mid(s,pos1+8,pos2-pos1-8)
					callback_iothub_set_datetime(s)
				end if
			end if
			do
				s=right(s,64)+sock.tlsgetdata(191)
				pos1=instr(1,s,"assignedHub",1)
				if pos1>0 then
					pos2=instr(pos1,s,",",1)
					if pos2>0 then
						sock.rxclear()
						s=mid(s,pos1+14,pos2-pos1-15)
						callback_iothub_device_assigned(s)
						iothub_assigned_hub=s
						iothub_state=EN_IOTHUB_REGISTERED
						pos1=0
						pos2=0
					end if
				end if
			loop while pos1>0 or pos2>0 and sock.tlsrxlen>0
			
			do 
				s=sock.tlsgetdata(255)
			loop while sock.tlsrxlen>0
		end if			
	case EN_IOTHUB_PROVISIONING:
		if sock.num<>iothub_dps_sock then
			exit sub
		end if
		s=sock.tlsgetdata(255)
		do	
			pos1=instr(1,s,"\x22status\x22:\x22assigning\x22",1)
			if pos1>0 then
				pos2=instr(pos1,s,",",1)
				if pos2>0 then
					iothub_timer=10
					sock.rxclear()
					pos1=0
					pos2=0
				end if
			end if
			s=right(s,64)+sock.tlsgetdata(191)
		loop while pos1>0 or pos2>0 and sock.tlsrxlen>0	
	case else
		mqtt_proc_data()
		
	end select
		
end sub

function iothub_get_descriptor_field(line_end_pos as word, byref field_start_pos as word) as string
'line_end_pos is an INPUT parameter, field_start_pos RETURNS the field position 
	const FIELD_SEPARATOR="\x09"
	dim i as word
	
	do
		if romfile.pointer>=line_end_pos then
			'the field is missing
			iothub_get_descriptor_field=""
			field_start_pos=0
			exit function
		end if
		
		i=romfile.find(romfile.pointer,FIELD_SEPARATOR,1)
		field_start_pos=romfile.pointer
		if i=0 or i>line_end_pos then
			'no next field separator found on this line, so we assume the field goes to the end of the line
			i=line_end_pos
			iothub_get_descriptor_field=romfile.getdata(i-romfile.pointer)
			romfile.pointer=line_end_pos+1
		else
			iothub_get_descriptor_field=romfile.getdata(i-romfile.pointer)
			romfile.pointer=i+1
		end if
	loop while iothub_get_descriptor_field="" 		
end function

public function iothub_json_helper(byref original as string, byref attribute as string, byref value as string) as string
	if original="" then
		original="{}"
	end if
	
	while right(original,1)<>"}"
		original=left(original,len(original)-1)
	wend
		
	if original="{}" then
		if left(value,1)="{" and right(value,1)="}" then
			iothub_json_helper="{\x22"+attribute+"\x22:"+value+"}"
		else
			iothub_json_helper="{\x22"+attribute+"\x22:\x22"+value+"\x22}"
		end if
	else
		if left(value,1)="{" and right(value,1)="}" then
			iothub_json_helper=left(original,len(original)-1)+","+"\x22"+attribute+"\x22:"+value+"}"
		else
			iothub_json_helper=left(original,len(original)-1)+","+"\x22"+attribute+"\x22:\x22"+value+"\x22}"
		end if
	end if
end function

public sub iothub_mqtt_sub_ok()
	num_of_suback=num_of_suback+1
	if num_of_suback>4 then
		iothub_rid=iothub_rid+1
		mqtt_publish(TOPIC_TWIN+"GET/?$rid="+str(iothub_rid),"{}",QOS_AT_MOST_ONCE)
	end if
end sub

public sub iothub_on_ip_otained()

    http_request(HTTP_POST, "https://" + iothub_dps_server + "/" + iothub_scope_id+"/registrations/"+iothub_device_id+"?api-version=2019-03-31", PL_SOCK_INTERFACE_WLN,"{registrationId: \x222o0jgbdlqh2\x22}")
	
	
end sub

public sub iothub_on_telemetry_sent()
	if iothub_sending_telemetry=NO or sock.num<>mqtt_sock_num then
		exit sub
	end if
	iothub_sending_telemetry=NO
	callback_on_telemetry_sent()
end sub

public sub iothub_dps_connect(byref target_ip as string)

	sock.num=iothub_dps_sock
	sock.tlsdeinit()
	if target_ip<>"" then
		sock.targetip=ddstr(target_ip)
	end if
	sock.targetinterface=iothub_interface
	romfile.open(IOTHUB_DPS_TLS_CER)
	sock.tlsinit(romfile.offset)  'check return state
	sock.rxclear()
	sock.txclear()	
	sock.connect()
	
end sub

sub iothub_dps_device_lookup()
	dim content as string = "{registrationId: \x22"+iothub_device_id+"\x22}"
	
	sock.num=iothub_dps_sock
	if sock.state<>PL_SST_EST_TLS then
		exit sub
	end if
	sys.debugprint("DPS device lookup\x0d\x0a")
	
	sock.tlssetdata("POST /"+iothub_scope_id+"/registrations/"+iothub_device_id+"?api-version=2019-03-31 HTTP/1.1\x0d\x0a")
	sock.tlssetdata("Host: "+iothub_dps_server+"\x0d\x0a")
	sock.tlssetdata("Content-Length: "+str(len(content))+"\x0d\x0a")

	sock.tlssetdata("Content-Type: application/json\x0d\x0a")
	sock.tlssetdata("Authorization: SharedAccessSignature sr="+iothub_scope_id+"%2Fregistrations%2F"+iothub_device_id+"&sig=")
	sock.tlssetdata(iothub_dps_authorization)
	sock.tlssetdata("\x0d\x0a\x0d\x0a")
	sock.tlssetdata(content+"\x0d\x0a")
	sock.send()

end sub

sub iothub_dps_device_provisioning()
	dim content as string = "{registrationId: \x22"+iothub_device_id+"\x22}"
	
	sock.num=iothub_dps_sock
	if sock.state<>PL_SST_EST_TLS then
		exit sub
	end if

	sys.debugprint("DPS device provisioning\x0d\x0a")
	sock.tlssetdata("PUT /"+iothub_scope_id+"/registrations/"+iothub_device_id+"/register?api-version=2019-03-31 HTTP/1.1\x0d\x0a")

	sock.tlssetdata("Host: "+iothub_dps_server+"\x0d\x0a")
	sock.tlssetdata("Content-Length: "+str(len(content))+"\x0d\x0a")

	sock.tlssetdata("Content-Type: application/json\x0d\x0a")
	sock.tlssetdata("Authorization: SharedAccessSignature sr="+iothub_scope_id+"%2Fregistrations%2F"+iothub_device_id+"&sig=")
	sock.tlssetdata(iothub_dps_authorization)
	sock.tlssetdata("\x0d\x0a\x0d\x0a")	
	sock.tlssetdata(content+"\x0d\x0a")
	sock.send()

end sub

public sub iothub_dns_answer_acquired(return_type as en_dns_return_type, byref return_string as string)
	if return_type=EN_DNS_RET_IP then
		iothub_dps_connect(return_string)
	end if
end sub

sub iot_send_headers()

  if http_request_item.url = "https://global.azure-devices-provisioning.net/0ne002A0695/registrations/2o0jgbdlqh2?api-version=2019-03-31" then
        http_send_headers("Content-Type","application/json")
		http_send_headers("Authorization", "SharedAccessSignature sr="+iothub_scope_id+"%2Fregistrations%2F"+iothub_device_id+"&sig="+iothub_dps_authorization)
    end if
	
end sub

#if IOTHUB_DEBUG_PRINT
	sub iothub_debugprint(byref print_data as string)
		sys.debugprint("IOTHUB> "+print_data+"\x0d\x0a")
	end sub
#endif	



